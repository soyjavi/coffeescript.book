# 4. Objetos y Arrays
Ya dominas las funciones con CoffeeScript, ahora toca descubrir como utilizar tus funciones con colecciones de datos: los objetos y *Arrays*.

Comenzaremos echando un vistazo a los objetos como una buena propuesta para almacenar datos. Tras esto aprenderás como utilizar las listas, las cuales te darán un buen mecanismo para ordenar nuestros datos. Desde aquí comenzaremos con las estructuras iterativas loop, las comprensiones y la creación directa de arrays gracias a ellas. Selección de matrices en base a matching... y mucho mas.

## 4.1 Recordando JavaScript y sus ObjetosAntes de comenzar con la sintaxis CoffeeScript, será mejor que demos un breve repaso a los objetos en JavaScript, reconociendo que casi todo es un objeto; las únicas excepciones son las primitivas: *boolean*, *number* y *string* y las constantes *undefined* y *NaN*. El objeto más simple se escribiría:```
var object = new Object();
```

Aunque comunmente se suele utilizar la sintaxis heredada de [JSON](http://json.org):```var object = {};```
Existen muchísimas otras formas de creación de objetos, es más en el capítulo anterior hemos estado utilizando algunas de ellas, puesto que todas las funciones son en si objetos (puesto que no son ni *boolean*, *number*, *string*, *undefined* o *NaN*). 

Para acceder a las propiedades de un objeto tenemos dos formas, con la notación simple `.` o con la notación tipo bracket `{}`. La primera es sumamente sencilla de utilizar: `obj.x` hace referencia a la propiedad `x` del objeto `obj`. La notación via bracket es mucho más versátil, puesto que cualquier expresión que se incluya entre los brackets se evaluará y converirá a un string utilizándose como nombre de propiedad, veamos el mismo ejemplo `obj['x']`

Normalmente, utilizarás la notación por punto si conoces el nombre de la propiedad previamente y en el caso de que lo tengas que determinar dinámicamente utilizarás la notación por bracket. Pero no siempre el primer caso funciona:

```
symbols.* = 'plus' # Falla!!
symbols.['*'] = 'plus' ```

La primera sentencia falla ya que `+` esta reservado como operados, mientras que la segunda sentencia al ser pasada como *string* no tiene ese problema. Creo que por ahora no hace falta que recordemos más particularidades de JavaScript.


## 4.2 ObjetosLos objetos en CoffeeScript se pueden crear exactamente igual que en JavaScript, con nuestras queridas *Curly Braces* y una lista de declaraciones de Clave/Valor:
```
numbers = {one: 1, two: 2}
```Sin embargo, al igual que en la invocación de funciones, CoffeeScript nos permite aligerar nuestro código dejándonos a nuestra elección la utilización de los caracteres `{}`. Personalmente, las quito siempre y además utilizo el nivel de indentación con las nuevas lineas como sustituto del separador `,` de propiedades:
```
numbers = 
  one: 1
  two: 2```Con la exclusión completa de `{}` y `,` conseguimos que nuestro objeto sea más parecido a un [YAML](http://yaml.org/):

```
kids =
  brother:
    name: "Max"
    age:  11
  sister:
    name: "Ida"
    age:  9
```Este ejemplo seguro que te hará sonreir ademas de ver el verdadero potencial de la creació de objetos en una linea:
```
fellowship = wizard: 'Gandalf', hobbits: ['Frodo', 'Pippin', 'Sam']
```

La magia de CoffeeScript se encuentra en que cada vez que encuentra el caracter `:` sabe que estás haciendo referencia  a un objeto. Esta técnica es especialmente útil cuando una función tiene un argumeno que es un objeto:

```
findPeople latitude, longitude, order: true
```

Compilando a JavaScript de la siguiente manera:

```
drawSprite(latitude, longitude, {order: true});
```

Aunque ya tienes una base de JavaScript, vale recordar el uso de `this` en esta ocasión vamos a crear un objeto que tenga una función `toString` que devuelva serializado alguna de las propiedades de nuestro objeto. Veamos como:

```
podcast =
  number     : 11
  title      : '¿Porqué es dificil testear?'
  description: 'Conversación con Javier Acero sobre testear y programar.'
  details:
    homepage : 'http://www.bastayadepicar.com'
    url      : 'http://www.bastayadepicar.com/episodio/011'
  toString: -> "#{@number}. #{@title}"
```

Como ves existe el caracter `@` que realmente hace referencia a `this` que como ya sabemos esta marcando que el contexto es el suyo mismo (dado que es una flecha fina `->`). `@` no deja de ser un facilitador más de CoffeeScript. Una de las cosas que siempre recomiendo a la hora de trabajar con CoffeeScript es ver el código compilado para analizar las estructuras JavaScript que genera, analicemos pues:

```
var podcast;
podcast = {
  number: 11,
  title: '¿Porqué es dificil testear?',
  description: 'Conversación con Javier Acero sobre testear y programar.',
  details: {
    homepage: 'http://www.bastayadepicar.com',
    url: 'http://www.bastayadepicar.com/episodio/011'
  },
  toString: function() {
    return "" + this.number + ". " + this.title;
  }
};
```

En JavaScript, no puedes usar palabras reservadas como atributos de un objeto, por ejemplo `class`. CoffeeScript si te permite hacerlo puesto que todos los accesos a las propiedades los realizará por notación *bracket*. Veamos el ejemplo `module.class = 'atom'` el cual compilado a JavaScript será `module['class'] = 'atom';`.

Un último apunte sobre objetos en CoffeeScript, recomiendo encarecidamente el uso de la expresión `of` para conocer si una propiedad existe dentro de un determinado objeto:

```
console.log wizard of fellowship
```


## 4.3 Arrays
Los arrays siguen requiriendo los caracteres `[]` para indicar sus elementos, por lo que tampoco vas a encontrar mucha diferencia con JavaScript. De todas formas puedes utilizar el espacio como delimitador de elementos y podrás finalizar con `,` algo que en JavaScript no está permitido y suele dar bastantes quebraderos de cabeza. Veamos unos ejemplos:

```
numbers = [1, 2, 3]
letters = [
  'A'
  'B'
  'C'
]
axys = [x, y, z,]
```

Posiblemente pensarás que no puedo hablar mucho más de Arrays, nada más lejos de la realidad a partir de ahora voy a poner casos de uso en combinación con funcionalidades de JavaScript y CoffeeScript.


### Obtener el valor máximo de un array
Imagina que tienes un array numérico y te gustaría obtener el valor máximo, seguramente ahora estarás pensando en una iteracción en JavaScript, una variable que va quedandose con el mayor numero y al final de la iteracción (evidentemente) tendrás el mayor. Bien vamos a hacerlo al estilo CoffeeScript:

```
Math.max [12, 32, 11, 67, 1, 3]...
```


Math.max compares every argument and returns the largest number from arguments. The ellipsis (...) converts every array value into argument which is given to the function. You can also use it with other functions which take variable amount of arguments, such as console.log.

### Mapeando Arrays
Imagina, tienes un array de objetos y quieres crear otro array tomando ese como base. Lo mismo que el caso anterior, posiblemente te pondrás a hacer iteraciones, acumuladores... Vamos a ver que puede hacer CoffeeScript por nosotros:

```
movies = [ 
  name: "Batman", year: 1991, hero: true
,
  name: "Spiderman", year: 2003, hero: true
,
  name: "Superman", year: 1984, hero: true
,
  name: "KickAss", year: 2011, hero: false
]
heroes = movies.map (movie) -> movie.name if movie.hero is true
```

Como ves he utilizado la conocida función `map` que nos permite hacer operaciones con los elementos de un array. Como puedes leer, porque una cosa que nos da CoffeeScript es la comprensión sin conocimiento, en el nuevo array `heroes` solo existiran los *strings*  `'Superman'`, `'Batman'` y `'Spiderman'` ya que estamos filtrando por la propiedad `hero`. Como has podido comprobar tambien he eliminado las *Curly Braces*, y he marcado la dimensión de cada objeto, con un nivel de indentación (fijate que la `,` esta en un nivel inferior).

Los mapeos son una buena herramienta para manejar transformaciones más complicadas que la que hemos realizado, más adelante conocerás otra manera de hacerlo por medio de *comprensiones*.

### Reduciendo Arrays
Imagina, tenemos un array de elementos y nos gustaria hacer algun tipo de operación por todos los elementos

```
[1,2,3,4].reduce (x,y) -> x + y
> 10
["words", "of", "bunch", "A"].reduceRight (x, y) -> x + " " + y
> 'A bunch of words'
```

Or it may be something more complex such as aggregating elements from a list into a combined object.

```
people =
    { name: 'alec', age: 10 }
    { name: 'bert', age: 16 }
    { name: 'chad', age: 17 }
people.reduce (x, y) ->
    x[y.name]= y.age
    x
, {}
> { alec: 10, bert: 16, chad: 17 }
```

Javascript introduced `reduce` and `reduceRight` in version 1.8. Coffeescript provides a natural and simple way to express anonymous functions. Both go together cleanly in the problem of merging a collection's items into a combined result.

### Rangos de Arrays
Imagina, que tienes que crear un array que contenta una serie numerica. Deja de hacer más iteraciones y conoce un nuevo operador en CoffeeScript:

```
numbers = [1..10]
> [ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 ]
numbers = [10..1]
> [ 10, 9, 8, 7, 6, 5, 4, 3, 2, 1 ]
```

Cuando escribimos el operador `..` CoffeeScript interpreta que necesitamos una serie numerica entre el primer (1) y el ultimo valor (10). Si queremos omitir el ultimo valor dentro de la serie tendrémos que utilizar el operador `...` como vemos en el siguiente código:

```
numbers = [1...10]
> [ 1, 2, 3, 4, 5, 6, 7, 8, 9 ]
```

### Filtrando Arrays
Imagina, necesitas filtrar un array por una determinada condición booleana.

```
numbers = [1..10]
numbers.filter (number) -> number > 5
```

En el método `filter` vemos que define como parametro una función en la cual estamos pidiendo que se quede con los numeros del array mayores que 5 por lo que tendremos el resultado `[6,7,8,9,10]`.

### Uniendo Arrays
Imagina, necesitas unir dos arrays en una sola y no quieres crear un iterador, mira que sencillo:

```
numbers_1 = [1, 2, 3]
numbers_2 = [4, 5, 6]
numbers = numbers_1.concat numbers_2
> [1, 2, 3, 4, 5, 6]
```

Realmente `concat`es una función de JavaScript, pero mucha gente no la utiliza y es realmente util. La gran diferencia con el método `push` es que `concat` crea un nuevo array y deja los que se tomaron como base sin modificación.


## 4.4 Comprensiones
Como hemos visto anteriormente podemos hacer mapeos de funciones de una manera muy sencilla, pero CoffeeScript nos da las comprensiones un tipo de operación que los desarrolladores de Python podrán reconocerlas facilmente. Vamos a volver a realizar un mapeo mediante comprension de nuestra conocida array `heroes`:

```
movies = [ 
  name: "Batman", year: 1991, hero: true
,
  name: "Spiderman", year: 2003, hero: true
,
  name: "Superman", year: 1984, hero: true
,
  name: "KickAss", year: 2011, hero: false
]
heroes = (movie.name for movie in movies when movie.hero)
> [ 'Doctor Teeth', 'Janice', 'Sgt. Floyd Pepper', 'Zoot', 'Lips', 'Animal' ]
```

Como puedes comprobar es más sencillo que utilizar el método `map` y sobre todo la creación del array `heroes` es realmente comprensible (leélo y dime que no). 

Si todavía no te he convencido vamos a ver otro ejemplo de dos maneras, la aburrida y la divertida (jedy-mode). Teniendo un array `heroes`:

```
heroes = ['Batman', 'Spiderman', 'Superman']
```

Muestro por pantalla unos heroes determinados, pero para ello escribo 3 lineas de código:

```
for hero, index in heroes
  if index % 2 == 0
    console.log(hero)
```

Ahora vas a hacerlo como un verdadero Jedi, sintiendo la fuerza y elegancia que ofrece CoffeeScript:

```
console.log hero for hero, index in heroes when index % 2 is 0
```

Como vemos, dentro del `for` podemos además de tener cada elemento en `hero`, podemos saber el indice del mismo como segundo argumento (en este caso `index`). Con lo que utilizamos este segundo argumento para hacer la función de filtrado. Ahora ya puedes ir a tu consola de CoffeeScript para jugar con tus heroes y comprensiones.

Si todavía no estas impresionado por esta capacidad que tiene este lenguaje, solo me queda sorprendente con una implementación del FizzBuzz en una única linea:

```
fizzbuzz = (['fizz' unless i%3] + ['buzz' unless i%5] or i for i in [1..100])
console.log fizzbuzz
```

Esta implementación no es mia proviene del blog de [Ricardo Tomasi](http://ricardo.cc/), el cual recomiendo que le eches un vistazo.